description = 'spitter'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-tomcat-plugin:1.0'
    }
}

apply plugin: 'idea'

allprojects {

    group = 'com.rafalkalita.spitter'
    version = '0.2.0.SNAPSHOT'

    // default set of maven repositories to be used when resolving dependencies
    repositories {
        maven {
            url "http://maven.springframework.org/release"
        }
        maven {
            url "http://maven.springframework.org/milestone"
        }
        maven {
            url "http://maven.springframework.org/snapshot"
        }
        mavenCentral()
    }
}

configure(subprojects) {

    apply plugin: 'java'
    apply plugin: 'maven'

    project.ext {

        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7

        jacksonVersion = '1.9.13'
        jodaVersion = '2.3'
        junitVersion = '4.10'
        mockitoVersion = '1.9.5'
        springVersion = '3.2.6.RELEASE'
        hibernateVersion = '3.6.10.Final'
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
            resources {
                srcDirs = ['src/main/resources', 'src/main/webapp']
            }
        }
        test {
            java {
                srcDir 'src/test/java'
            }
            resources {
                srcDirs = ['src/test/resources']
            }
        }
    }

    configurations {
        provided { transitive = false }
    }

    sourceSets.main.compileClasspath += configurations.provided
    sourceSets.test.compileClasspath += configurations.provided
    sourceSets.test.runtimeClasspath += configurations.provided

    dependencies {
        compile 'log4j:log4j:1.2.17'
        compile 'org.slf4j:slf4j-simple:1.6.6'
        compile 'javax.inject:javax.inject:1'
        testCompile "junit:junit:$junitVersion"
        testCompile "org.mockito:mockito-all:$mockitoVersion"
        testCompile "org.springframework:spring-test:$springVersion"

        testCompile 'mysql:mysql-connector-java:5.1.28'
        testCompile 'joda-time:joda-time:2.3'
    }

    [compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:all']

    test {
        testLogging {
            // Show that tests are run in the command-line output
            events 'started', 'passed'
        }
    }
}

project('spitter-model') {

    description = 'Spitter Model'
    dependencies {
        compile "commons-lang:commons-lang:2.6"
        compile "javax.persistence:persistence-api:1.0"
        compile "javax.validation:validation-api:1.0.0.GA"
        compile "org.codehaus.jackson:jackson-core-asl:1.6.3"

        compile "org.springframework:spring-context:$springVersion"
        compile 'org.hibernate:hibernate-validator:5.0.2.Final'
    }
}

project('spitter-persistence') {

    description = 'Spitter Persistence'
    dependencies {
        compile project(':spitter-model')
    }
}

project('spitter-persistence-hibernate') {

    description = 'Spitter Persistence - Hibernate implementation'
    dependencies {
        compile project(':spitter-model')
        compile project(':spitter-persistence')

        compile "org.hibernate:hibernate-core:$hibernateVersion"
        compile "org.hibernate:hibernate-entitymanager:$hibernateVersion"
        compile 'org.hibernate:hibernate-annotations:3.5.6-Final'
        compile 'org.hibernate:hibernate-commons-annotations:3.2.0.Final'

        compile "org.springframework:spring-jdbc:$springVersion"
        compile "org.springframework:spring-orm:$springVersion"

        compile 'mysql:mysql-connector-java:5.1.28'

        testCompile 'org.hsqldb:hsqldb:2.3.1'
    }
}

project('spitter-service') {

    description = 'Spitter Service'
    dependencies {
        compile project(':spitter-model')
        compile project(':spitter-persistence')
        compile project(':spitter-persistence-hibernate')

        compile "org.springframework:spring-webmvc:$springVersion"
    }
}

project('spitter-web') {

    apply plugin: 'tomcat'
    description = 'Spitter Web'

    dependencies {

        compile project(':spitter-model')
        compile project(':spitter-service')

        def tomcatVersion = '7.0.11'
        tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
                "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
        tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}") {
            exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
        }

        //Compile time but not included dependencies
        providedCompile group: 'org.jboss.spec.javax.servlet', name:'jboss-servlet-api_3.1_spec', version:'1.0.0.Final'
        providedCompile group: 'taglibs', name: 'standard', version: '1.1.2'

        providedCompile 'org.apache.tomcat:jsp-api:6.0.37'
        providedCompile 'org.apache.tomcat:jasper:6.0.37'

        compile "org.springframework:spring-webmvc:$springVersion"

        compile 'org.springframework.security:spring-security-core:3.2.0.RELEASE'
        compile "org.springframework.security:spring-security-web:3.2.0.RELEASE"
        compile "org.springframework.security:spring-security-config:3.2.0.RELEASE"
        compile 'org.springframework.security:spring-security-taglibs:3.2.0.RELEASE'

        compile 'org.apache.tiles:tiles-core:2.2.2'
        compile 'org.apache.tiles:tiles-jsp:2.2.2'
        compile 'jstl:jstl:1.2'
    }

    tomcatRun {
        httpPort = 8080
        httpsPort = 8091
        enableSSL = true
    }

    [tomcatRun, tomcatStop]*.stopPort = 8081
    [tomcatRun, tomcatStop]*.stopKey = 'stopKey'
    //[tomcatRun, tomcatStop]*.gradleOpts = '-Xdebug -Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005'

    task createPom << {
        pom {
            project {
                groupId $group
                artifactId 'spitter'
                version $version

                inceptionYear '2013'

            }
        }.writeTo("pom.xml")
    }
}

project('spitter-specification') {

    description = 'Spitter Specification'

    dependencies {
        compile project(':spitter-service')

        compile 'org.jbehave:jbehave-core:4.0-beta-3'

        compile 'org.jbehave.web:jbehave-web-selenium:3.6-beta-1'
        compile 'org.jbehave.site:jbehave-site-resources:3.1.1'
        compile 'org.jbehave:jbehave:3.9'
        compile 'org.jbehave.web:jbehave-web:3.5.5'
        compile 'org.jbehave:jbehave-spring:3.9'

        compile 'org.seleniumhq.selenium:selenium-java:2.35.0'
        compile 'org.seleniumhq.selenium.fluent:fluent-selenium:1.14.2'

        compile "org.springframework:spring-jdbc:$springVersion"
    }

    test {
        onlyIf { false }
    }
}
